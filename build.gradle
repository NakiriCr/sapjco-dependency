import java.util.stream.Collectors

plugins {
    id 'java'
}
apply plugin: 'maven-publish'
apply plugin: 'signing'

apply from: 'sapjco.gradle'

group 'cn.yanzx-dev.gitlab'
version artifactVersion

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://maven.mit.edu/nexus/content/repositories/public/" }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"
}

dependencies {
    compileOnly group: 'com.sap.jco', name: 'sapjco', version: sapjcoVersion

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    from { 
        configurations.compileOnly.collect { it.isDirectory() ? it : zipTree(it) }
                .stream()
                .map { tree -> 
                    tree.matching { exclude 'META-INF/MANIFEST.MF', 'META-INF/SAP_SE_G.RSA', 'META-INF/SAP_SE_G.SF' } }
                .collect(Collectors.toList())
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'cn.yanzx-dev.gitlab'
            artifactId = 'com.sap.conn.jco.sapjco'
            version = artifactVersion
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                // artifact name
                name = 'Sapjco Runtime Dependency'
                // artifact description
                description = 'Sapjco Runtime Dependency'
                // artifact homepage
                url = 'https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-dependency'
                // licenses
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // developers info
                developers {
                    developer {
                        name = 'VirtualCry'
                        email = '172519267@qq.com'
                    }
                }
                // scm
                scm {
                    url = 'https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-dependency'
                    connection = 'scm:git:https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-dependency.git'
                    developerConnection = 'scm:git:https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-dependency.git'
                }
            }
        }
    }
    // repositories to publish
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
